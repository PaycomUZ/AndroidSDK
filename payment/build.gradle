apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

ext {
  code = 1
  ver  = "1.0.5.4"
}

android {
  defaultConfig {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion
    minSdkVersion project.ext.minSdkVersion
    versionCode code
    versionName ver
  }
  buildTypes {
    debug {
      debuggable true
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-payment.pro'
    }
  }
}

dependencies {
  implementation "com.android.support:appcompat-v7:$supportVersion"
}

group = 'uz.paycom'
version = ver

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")
  configurations = ['archives']
  pkg {
    repo = 'general'
    name = 'android-sdk'
    userOrg = 'paycom'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/PaycomUZ/AndroidSDK.git'
    version {
      name = ver
      desc = 'Paycom android sdk'
      vcsTag = ver
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}